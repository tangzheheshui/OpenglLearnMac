#version 330 core
out vec4 FragColor;

in VS_OUT {
    vec2 TexCoord;
    vec3 TangentLightPos;
    vec3 TangentViewPos;
    vec3 TangentFragPos;
    vec4 FragPosLightSpace;
} fs_in;

struct Light {
    vec4 position;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

uniform Light uLight;
uniform sampler2D uTexture;
uniform sampler2D uTextureShadowMap;
uniform sampler2D uTextureNormal;

float ShadowCalculation(vec4 fragPosLightSpace)
{
    vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
    projCoords = projCoords * 0.5 + 0.5;
    float currentDepth = projCoords.z;
    float shadow = 0.f;
    
    vec2 texelSize = 1.0 / textureSize(uTextureShadowMap, 0);
    for(int x = -1; x <= 1; ++x)
    {
        for(int y = -1; y <= 1; ++y)
        {
            float pcfDepth = texture(uTextureShadowMap, projCoords.xy + vec2(x, y) * texelSize).r; 
            shadow += currentDepth > pcfDepth ? 1.0 : 0.0;        
        }    
    }
    shadow /= 9.0;
    return shadow;
}

void main()
{
    vec3 color = texture(uTexture, fs_in.TexCoord).xyz;
    vec3 normal = texture(uTextureNormal, fs_in.TexCoord).rgb;
    normal = normalize(normal * 2.0 - 1.0); 
    
    // ambient
    vec3 ambient = uLight.ambient * color;
    
    // diffuse
    vec3 dirLight = normalize(fs_in.TangentLightPos - fs_in.TangentFragPos);
    float diff = max(dot(dirLight, normal), 0.0);
    vec3 diffuse = uLight.diffuse * color * diff;
    
    // specular
    vec3 viewDir = normalize(fs_in.TangentViewPos - fs_in.TangentFragPos);
    vec3 halfwayDir = normalize(dirLight + viewDir);  
    float spec = pow(max(dot(normal, halfwayDir), 0.0), 32.0);
    vec3 specular = uLight.specular * spec;
 
    vec3 result =  (ambient + diffuse + specular);
    
    // shadow
    float shadow = ShadowCalculation(fs_in.FragPosLightSpace); 
    float dark = 1 - shadow * 0.7;
    
    FragColor = vec4(dark * result, 1.0);
}
